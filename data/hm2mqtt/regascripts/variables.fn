!# variables.fn hm2mqtt
!#
!# Dieses Script gibt die Systemvariablen im JSON Format aus
!#
!# 3'2013-4'2017 hobbyquaker https://github.com/hobbyquaker
!#

object oSysVar;
string sSysVarId;
string sValueType;
string sValue;
boolean bFirst = true;

Write('{');

WriteLine('"40":{"name":"Alarmmeldungen","val":' # dom.GetObject(40).Value() # ',"min":0,"max":65000,"unit":"","type":16,"enum":""},');
Write('"41":{"name":"Servicemeldungen","val":' # dom.GetObject(41).Value() # ',"min":0,"max":65000,"unit":"","type":16,"enum":""}');

foreach (sSysVarId, dom.GetObject(ID_SYSTEM_VARIABLES).EnumUsedIDs()) {

  WriteLine(',');

  oSysVar     = dom.GetObject(sSysVarId);
  sValueType  = oSysVar.ValueType();

  Write('"' # sSysVarId # '":{"name":"');
  WriteURL(oSysVar.Name());
  Write('","val":');
  if (sValueType == 20) {
    Write('"');
  	WriteURL(oSysVar.Value());
    Write('"');
  } else {
    sValue = oSysVar.Value();
    if (sValueType == 2) {
        if (sValue) {
            Write("true");
        } else {
            Write("false");
        }
    } else {
       if (sValue == "") {
            Write("0");
       } else {
            Write(sValue);
       }
    }
  }
  string sValueMin = oSysVar.ValueMin();

  if (sValueMin == '') {
      sValueMin = 'null';
  }

  string sValueMax = oSysVar.ValueMax();

  if (sValueMax == '') {
      sValueMax = 'null';
  }

  Write(',"ts":"' # oSysVar.Timestamp());
  Write('","min":' # sValueMin # ',"max":' # sValueMax # ',"unit":"');
  WriteURL(oSysVar.ValueUnit());
  Write('","type":' # sValueType);

  if (sValueType == 2) {
    Write(',"enum":"');
    WriteURL(oSysVar.ValueName0() # ';' # oSysVar.ValueName1());
  } else {
    Write(',"enum":"');
    WriteURL(oSysVar.ValueList());
  }

  Write('"}');

}

Write('}');
